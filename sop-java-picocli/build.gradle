// SPDX-FileCopyrightText: 2021 Paul Schaub <vanitasvitae@fsfe.org>
//
// SPDX-License-Identifier: Apache-2.0

plugins {
    id 'application'
}

dependencies {
    // JUnit
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    // Testing Exit Codes in JUnit
    // https://todd.ginsberg.com/post/testing-system-exit/
    testImplementation "com.ginsberg:junit5-system-exit:$junitSysExitVersion"

    // Mocking Components
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    implementation(project(":sop-java"))
    implementation "info.picocli:picocli:4.6.3"

    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}

mainClassName = 'sop.cli.picocli.SopCLI'

application {
    mainClass = mainClassName
}

jar {
    dependsOn(":sop-java:jar")

    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

